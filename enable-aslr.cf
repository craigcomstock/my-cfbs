bundle agent enable_aslr
# @brief Makes sure that aslr (Address space layout randomization) is enabled on the system.
# Based on the OpenSCAP Security Guide for RHEL 9:
# https://static.open-scap.org/ssg-guides/ssg-rhel9-guide-index.html
# CFEngine policy based on this module:
# https://build.cfengine.com/modules/uninstall-rsh-server/
{
  vars:
    linux::
      # see https://www.kernel.org/doc/html/latest/admin-guide/sysctl/kernel.html#randomize-va-space
      # or `man procfs`
      # for description of the three modes: 0,1,2
      "aslr_mode[0]" string => "Disabled";
      "aslr_mode[1]" string => "Conservative Randomization";
      "aslr_mode[2]" string => "Full Randomization";

      "aslr_value" string => readfile("/proc/sys/kernel/randomize_va_space");
      "randomize_va_space_inventory" string => "${aslr_mode[${aslr_value}]} (${aslr_value})",
        meta => { "inventory", "attribute_name=Address space layout randomization (aslr)" },
        comment => "Report on Address space layout randomization (aslr) mode";

  classes:
    "aslr_enabled" expression => strcmp("${aslr_value}", "2");
    "aslr_exception_allowed"
      or => {
        "hardening_aslr_disabled_allowed",
        "data:hardening_aslr_disabled_allowed",
        "exception_enable_aslr",
        "data:exception_enable_aslr",
      };

  files:
    linux.!aslr_exception_allowed.!aslr_enabled::
      "/etc/sysctl.d/enable-aslr"
        content => "2",
        classes => if_repaired("enable_aslr_repaired");

  commands:
    enable_aslr_repaired::
      "sysctl --system"
        contain => in_shell;
}
