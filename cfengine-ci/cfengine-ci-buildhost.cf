bundle agent cfengine_ci_buildhost
{
  packages:
    debian_9|debian_10::
      "python-psycopg2";
    debian_11|debian_12::
      "python3-psycopg2";
    ubuntu_16:: # ubuntu_16 doesn't have systemd-coredump by default?
      "systemd-coredump";
    ubuntu_20::
      "autoconf"; # because on arm ubu20 we need to reconfigure
      "shellcheck"; # not sure why, ubu20 addition
    debian.(!debian_12.!ubuntu_22)::
      "python"; # debian_12 has only python3

# todo, is absent good enough and same as purge? or do we need to push harder?
    debian|ubuntu::
      "libltdl7" policy => "absent";
      "libltdl-dev" policy => "absent";

# todo --allow-change-held-packages introduced in debian-12, why?
# packages to install
      "binutils";
      "bison";
      "build-essential";
      "curl"; # added for debian-10
      "debhelper";
      "default-jre";
      "dpkg-dev";
      "expat";
      "fakeroot";
      "flex";
      "gdb";
      "libncurses5"; # added for debian-10
      "libncurses5-dev"; # added for debian-10
      "libexpat1-dev";
      "libmodule-load-conditional-perl";
      "libpam0g-dev";
      "ntp";
      "pkg-config";
      "psmisc";
#      "python"; # note on debian, always python2
      "python3-pip";
      "rsync"; # added for debian-10

  classes:
    debian_11::
      "have_pip2" expression => fileexists("/usr/local/bin/pip");
    ubuntu_16::
      "have_i386_architecture" expression => strcmp(execresult("${paths.dpkg} --print-foreign-architectures", "noshell"), "i386");
    ubuntu::
      "have_localhost_hostname" expression => strcmp(execresult("${paths.hostname}", "noshell"), "localhost.localdomain");

  files:
    debian_9::
      "/etc/apt/sources.list.d/*"
        delete => tidy;
      "/etc/apt/sources.list"
        content => "deb http://archive.debian.org/debian/ stretch main contrib non-free";

  commands:
    debian_11.!have_pip2::
      "wget https://bootstrap.pypa.io/pip/2.7/get-pip.py -O get-pip.py && python2 get-pip.py && pip install psycopg2-binary"
        contain => in_shell;

    ubuntu_16.!have_i386_architecture:: # mingw build host
      "${paths.dpkg} --add-architecture i386";

    ubuntu.!have_localhost_hostname:: # hack for aws ubuntu hosts having unique ip-n-n-n-n hostnames, we need localhost.localdomain
      "/usr/bin/hostnamectl set-hostname localhost.localdomain";

# skip /etc/hosts change for now, seems kind of wrong and corrupts ip6 entries like `::1 ip6-ip6-loopback`
#    ubuntu::
#      "${paths.sed} -ri 's/localhost //' /etc/hosts";
}
